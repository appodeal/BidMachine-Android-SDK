/**
 * Gradle task for submit aar build to publish repo.
 * Required params:
 * repo - target publish repository
 * uname - user name for authorization
 * upassword - user password for authorization
 *
 * Example: 'YOUR_REQUIRED_TASK_NAME' -Partifactory_repo='YOUR_REPOSITORY_NAME' -Partifactory_username='YOUR_USER_NAME" -Partifactory_password='YOUR_ARTIFACTORY_USER_NAME'
 */

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def libGroupId = "io.bidmachine"
def libArtifactId = "ads"

artifactory {
    contextUrl = 'https://artifactory.bidmachine.io/artifactory'
    publish {
        repository {
            if (project.hasProperty("repo")) {
                repoKey = repo
            }
            if (project.hasProperty("uname")) {
                username = uname
            }
            if (project.hasProperty("upassword")) {
                password = upassword
            }
        }
        defaults {
            publications("_${project.name}_")
            publishArtifacts = true
            publishPom = true
        }
    }
}

publishing {
    publications {
        "_${project.name}_"(MavenPublication) {
            groupId = libGroupId
            artifactId = libArtifactId
            version = android.defaultConfig.versionName
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Protobuf models
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'io.bidmachine')
                dependencyNode.appendNode('artifactId', 'protobuf-models')
                dependencyNode.appendNode('version', protobufModelsVersion)
                // Stack IAB
                dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.explorestack')
                dependencyNode.appendNode('artifactId', 'iab')
                dependencyNode.appendNode('version', stackIabVersion)
            }
        }
    }
}

task uploadAarToArtifactory(dependsOn: [assemble, artifactoryPublish], group: 'aar')
artifactoryPublish.mustRunAfter(assemble)

task uploadLocalAar(dependsOn: [assemble, publishToMavenLocal], group: 'aar')
uploadLocalAar.mustRunAfter(assemble)
