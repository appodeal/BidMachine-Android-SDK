/**
 * Gradle task for submit aar build to publish repo.
 * Required params:
 * artifactory_repo - target publish repository
 * artifactory_username - user name for authorization
 * artifactory_password - user password for authorization
 */

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def libGroupId = "io.bidmachine"
def libArtifactId = "ads"

configurations.maybeCreate("default")
def publishArtifact = artifacts.add("default", file("build/outputs/aar/bidmachine-release.aar"))

publishing {
    publications {
        aar(MavenPublication) {
            groupId = libGroupId
            artifactId = libArtifactId
            version = android.defaultConfig.versionName
            artifact publishArtifact

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.explorestack')
                dependencyNode.appendNode('artifactId', 'iab')
                dependencyNode.appendNode('version', stackIabVersion)
            }
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.bidmachine.io/artifactory'
    publish {
        repository {
            if (project.hasProperty("artifactory_repo")) {
                repoKey = artifactory_repo
                println "Artifactory repo: $artifactory_repo"
            } else {
                println "Artifactory repo not provided (-Partifactory_repo)"
            }
            if (project.hasProperty("artifactory_username")) {
                username = artifactory_username
                println "Artifactory username: $artifactory_username"
            } else {
                println "Artifactory username not provided! (-Partifactory_username)"
            }
            if (project.hasProperty("artifactory_password")) {
                password = artifactory_password
                println "Artifactory password: $artifactory_password"
            } else {
                println "Artifactory password not provided! (-Partifactory_password)"
            }
        }
        defaults {
            publications('aar')
        }
    }
}

task uploadAarToArtifactory(dependsOn: [assemble, artifactoryPublish], group: 'aar')
artifactoryPublish.mustRunAfter(assemble)

task uploadLocalAar(dependsOn: [assemble, publishToMavenLocal], group: 'aar')
uploadLocalAar.mustRunAfter(assemble)
